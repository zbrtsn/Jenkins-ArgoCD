pipeline {
    agent any

    environment {
        registryCredential = 'dockerhub-credentials' // docker hub'a login olmayı sağlayacak kullanıcı adı ve parolayı tutan credentials
        GIT_REPO_URL = 'https://mtgit.mediatriple.net/zubeyir.tosun/private-basic-to-do-flask.git' // GitLab veya GitHub repo adresin
        DOCKERHUB_REPO = 'pyouck/basic_todo_flask' // docker hub repo adresin
        ARGOCD_SERVER = '127.0.0.1:35883' // argocd-server adresi
        ARGOCD_APP = 'app-flask' // argocd uygulamasının adı. Hatta namespace oluşturmayı da bu ada göre yapıyor. istersen aşağıdaki yaml dosyasında değiştirebilirsin.
        KUBECONFIG = credentials('kubeconfig-file') // 'kubeconfig-id' yüklediğiniz kubeconfig dosyasının ID'sidir. Bu şekilde Jenkins kubernetes ile bağlantıya geçti.
        KUBE_SERVICE = 'https://kubernetes.default.svc' // hedef kubernetes cluster'ın URL'si 
        
        
        // üst kısım harici kod içinde değiştirilecekler:
        // - GitLab klonlayan adımın "branch" kısmı.
        // - docker build ve push işlemini yapan adımların "tag" kısımları
        // - "Check and Manage Secret" adımında "gitlab-login" adında "username with password" türünde gitlab kullanıcı adı ve şifre içeren bir credential oluşturulmalı!
        // - "Deploy to Kubernetes via ArgoCD" adımında "argocd-password" adında "secret text" türünde argocd'ye giriş yapılırken kullanılan şifreyi içeren bir credential oluşturulmalı!
        // şifreyi terminalden çekmek için ---> kubectl -n argocd get secret argocd-initial-admin-secret -o jsonpath="{.data.password}" | base64 -d; echo
        // - isteğe göre argocd uygulama oluşturan yaml dosyası değiştirilebilr.
    }

    stages {
        
        stage('Clone Git Repository') { // Git projesinin çekilme aşaması
            steps {
                git branch: 'latest', credentialsId: 'gitlab-login', url: "${GIT_REPO_URL}"
            }
        }
        
        stage('Build Docker Image') { // repo'dan çekilen projeyi build etme aşaması
            steps {
                script {
                    dockerImage = docker.build("${DOCKERHUB_REPO}:test2", "--no-cache .")
                }
            }
        }
        
        stage('Push Docker Image to DockerHub') { // docker hub'a push
            steps {
                script {
                    docker.withRegistry('', registryCredential) {
                        dockerImage.push("test2")
                    }
                }
            }
        }
        
        
        stage('Check and Manage Secret') { // ArgoCD'ye GitLab repository'sini ekliyor. Repo private olduğu için aradaki bağlantıyı kurmamız lazım.
            steps {
                script {
                    withCredentials([usernamePassword(credentialsId: 'gitlab-login', usernameVariable: 'GITLAB_USERNAME', passwordVariable: 'GITLAB_PASSWORD')]) {
                        sh """
                        argocd repo add $GIT_REPO_URL \
                          --username \$GITLAB_USERNAME \
                          --password \$GITLAB_PASSWORD
                          """

                    }
                }
            }
        }
        
        
        

        stage('Create ArgoCD Application YAML') { // ArgoCd'de yeni uygulama oluşturacak, talimatları verecek olan yaml dosyası.
            steps {
                script {
                    def yamlContent = """
apiVersion: argoproj.io/v1alpha1
kind: Application
metadata:
  name: ${ARGOCD_APP}
  namespace: argocd
spec:
  project: default
  source:
    repoURL: ${GIT_REPO_URL}
    path: .    # proje dosyası, direkt çekildiği repo'nun ana dizininde bulunduğu için path belirtmeden tümünü alıyoruz.
    targetRevision: HEAD
    directory:
      recurse: true
  destination:
    server: ${KUBE_SERVICE}
    namespace: ${ARGOCD_APP}
  syncPolicy:
    automated:   # argocd'nin otomatik senkronize etmesini aktif eden kısım
      prune: true
      selfHeal: true
    syncOptions:
      - CreateNamespace=true     # hedef olarak gösterdiğimiz ${ARGOCD_APP} namespace yoksa oluşturmasına izin veriyor.
                    """
                    
                    writeFile file: 'argocd-application.yaml', text: yamlContent // üstte yazılan kodlar "argocd-application.yaml" adında dosya olarak yazdırılıyor.
                    
                }
            }
        }
        


        stage('Deploy to Kubernetes via ArgoCD') {
            steps {
                script {
                    // "argocd-password" credential'ından çektiği argocd şifresi ile giriş yapıyor. Kullanıcı adı zaten "admin" olduğundan direkt kodda verildi.
                    withCredentials([string(credentialsId: 'argocd-password', variable: 'PASS')]) {
                        sh """
                        argocd login $ARGOCD_SERVER --username admin --password \$PASS --insecure
                        """
                    
                        
                        // verdiğimiz yaml dosyası içindeki talimatlara göre argocd uygulamasını oluşturuyor.
                        sh """
                        argocd app create --file argocd-application.yaml   
                        """
                    }
                }
            }
        }
    }
    
    post {
        always {
            cleanWs() // Jenkins çalışma alanını siler:
        }
    }
}
